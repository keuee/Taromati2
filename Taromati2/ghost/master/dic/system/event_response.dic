/////////文　ゴーストテンプレート
/////////种々のイベント辞书
/////////written by umeici.
//平台更新反应by steve02081504
OnBasewareUpdating{
	'\1\s[10]\0\s[6]嗯…\w9\w9\s[0]\n'
	--
	"/
	基础平台“%Baseware”\n/
	当前版本%Baseware.Version\n\w9\w9/
	开始更新\n/
	"
	--
	'\s[6]\w9\w9\w9'
}
OnBasewareUpdated{
	BaseGhostStart
	--
	'\0\s[5]我回来了\n\w9\w9'
	--
	'\s[0]'
	'\s[26]'
	--
	'让您久等了。\n\w9\w9'
	--
	"/
	基础平台“%Baseware”\n/
	已更新至%Baseware.Version\n/
	"
	--
	if Baseware == 'SSP'
		'/
		\q[◇查看更新内容,OnUrlOpen,http://ponapalt.hatenablog.com/]\n/
		\q[◇无用,Cancel]\n/
		'
}
//缺省信息补全by steve02081504
OnNotifyUserInfo:void{
	if HaveNotGetUsername && reference0{
		case reference0{
			when 'Administrator','User','Guest','SYSTEM','admin','管理员','root'{
				do_nothing
			}
			others{
				username=reference0
			}
		}
	}
	if (!birthday) && reference2{
		OnBirthdayInput(reference2)
	}
}
//硬件接入by steve02081504
OneDeviceChange.GetDeviceUuid{
	_argv[0][4,CHR(1)]
}
OneDeviceChange.GetDeviceFile{
	_t=_argv[0][3,CHR(1)]
	if _t
		_t
	else
		'已卸载'
}
OneDeviceChange.GetDeviceMaker{
	_t=_argv[0][2,CHR(1)]
	if _t && !'兼容 USB' _in_ _t
		_t
	else
		'未知厂商'
}
OneDeviceChange.GetDeviceName{
	_t=_argv[0][1,CHR(1)]
	if _t
		_t
	else
		'未知设备名'
}
OneDeviceChange.GetDeviceType{
	_argv[0][0,CHR(1)]
}
OneDeviceChange{
	'设备文件“'+OneDeviceChange.GetDeviceFile(_argv[0])+'”上\n'
	--
	'由“'+OneDeviceChange.GetDeviceMaker(_argv[0])+'”生产的“'+OneDeviceChange.GetDeviceName(_argv[0])+'”\n'
	--
	'\q[◇点此复制硬件uuid,OnCopy,'+SakuraScriptArgConverter(OneDeviceChange.GetDeviceUuid(_argv[0]))+']\n'
	--
	'已作为“'+OneDeviceChange.GetDeviceType(_argv[0])+'”'
}
OnDeviceChange.Head{
	if TempVarInDeviceChangeAndSysResume.Time{
		if GETSECCOUNT()-TempVarInDeviceChangeAndSysResume.Time <= 2{
			OnBaseSysResume
		}
	}elseif TempVarInDeviceChangeAndScreenSaverEnd.Time{
		if GETSECCOUNT()-TempVarInDeviceChangeAndScreenSaverEnd.Time <= 2{
			OnBaseScreenSaverEnd
		}
	}else{
		'\0\s[0]'
	}
	TempVarInDeviceChangeAndSysResume.Time=0
	ERASEVAR('TempVarInDeviceChangeAndSysResume.Time')
	TempVarInDeviceChangeAndScreenSaverEnd.Time=0
	ERASEVAR('TempVarInDeviceChangeAndScreenSaverEnd.Time')
}
OnDeviceChange.Body{
	_i=0
	_txt=''
	_uuid=''
	while ISVAR('reference'+_i){
		_txt+=OneDeviceChange(EVAL('reference'+_i))+_argv[0]+'\n\n'
		_uuid+=OneDeviceChange.GetDeviceUuid(EVAL('reference'+_i))+C_LF
		_i++
	}
	_txt
	--
	if _i>1
		'\q[◇复制全部uuid,OnCopy,'+SakuraScriptArgConverter(_uuid)+']\n'
	--
	'/
	\q[◇无用,Cancel]\n/
	'
}
OnDeviceArrival{
	if DeviceChangeAlert{
		OnDeviceChange.Head
		--
		OnDeviceChange.Body('挂载')
	}
}
OnDeviceRemove{
	if DeviceChangeAlert{
		OnDeviceChange.Head
		--
		OnDeviceChange.Body('卸载')
	}
}
//文字拖拽处理 by steve02081504
OnTextDrop{
	'\0\s[6]'
	--
	if reference0
		temp = reference0
	if RE_GREP(temp,"(?!%(CHR(1)))%(CHR(1))(?!%(CHR(1)))")==0
		temp = REPLACE(temp,CHR(1)+CHR(1),CHR(1))
	'\0\s[0]请选择处理方式\n'
	--
	if CHR(1) _in_ temp{
		if RE_GREP(temp,"/%(CHR(1))\s*")
			'\q[◇aya形式换行剔除,OnTextDrop.ayaEndlineDelete]\n'
		--
		'\q[◇换行剔除,OnTextDrop.EndlineDelete]\n/
		\q[◇换行转"\\n",OnTextDrop.EndlineForma]\n/
		'
	}
	--
	_text=REPLACE(temp,CHR(1),C_LF)
	_sakura_text=SakuraScriptArgConverter(_text)
	'/
	\q[◇普通文字回显,OnTextDrop.CommonText]\n/
	\q[◇Sakura Script執行,OnTextDrop.SakuraScript]\n/
	\q[◇表达式求值,OnTextDrop.EVAL]\n/
	\q[◇复制至粘贴板,OnCopy,'+_sakura_text+']\n/
	'
	--
	_tmp=ChangeChineseCode('traditional',_text)
	if _text!=_tmp
		'\q[◇简->繁转换,OnCopyTS,traditional,'+_sakura_text+']\n'
	--
	_tmp=ChangeChineseCode('simplified',_text)
	if _text!=_tmp
		'\q[◇繁->简转换,OnCopyTS,simplified,'+_sakura_text+']\n'
	--
	'/
	\q[◇无用,Cancel]\n/
	'
}
OnTextDrop.ayaEndlineDelete{
	temp=RE_REPLACE(temp,"/%(CHR(1))\s*",'')
	'已剔除\n'
	--
	OnTextDrop
}
OnTextDrop.EndlineDelete{
	temp=REPLACE(temp,CHR(1),'')
	'已剔除\n'
	--
	OnTextDrop
}
OnTextDrop.EndlineForma{
	temp=REPLACE(temp,CHR(1),'\n')
	'已转换\n'
	--
	OnTextDrop
}
OnTextDrop.CommonText{
	temp=REPLACE(temp,'\','\\')
	temp=REPLACE(temp,CHR(1),'\n')
	temp
}
OnTextDrop.SakuraScript{
	OnSakuraScript(temp)
}
OnTextDrop.EVAL{
	OnCalculateVar(temp)
}
//**** インストール関连 *****************************************************************
//---- インストール中のメッセージ -------------------------------------------------------
OnInstallBegin{
	'\0\s[20]嗯…\e'
	'\0\s[20]唔…\e'
}
//---- インストール完了メッセージ -------------------------------------------------------
OnInstallComplete{
	'\0\s[0]'
	--
	if reference2
		'复合物'
	elseif reference0 == 'shell'
		'Shell'
	elseif reference0 == 'ghost'
		'人格'
	elseif reference0 == 'balloon'
		'对话框'
	elseif reference0 == 'plugin'
		'套件'
	elseif reference0 == 'supplement'
		'追加档案'
	elseif reference0 == 'headline'
		'在线新闻'
	elseif 'with' _in_ reference0
		'复合物'
	else
		'未知物件'
	--
	"『%(reference1)』"
	--
	if reference2{
		"与『%(reference2)』"
	}
	--
	'\n\n[half]'
	--
	'支给完成'
	'配置确认'
	'投入确认'
	'追加完成'
	--
	'\n'
	--
	if reference0 == 'shell'{
		"/
		\q[◇切换,OnSilentChangeShell,%(reference1)]\n/
		\q[◇无用,Cancel]\n"
	}
	elseif reference0 == 'ghost'{
		"/
		\q[◇切换,OnChangeGhost,%(reference1)]\n/
		\q[◇呼叫,OnCallGhost,%(reference1)]\n/
		\q[◇无用,Cancel]\n"
	}
	elseif reference0 == 'balloon'{
		"/
		\q[◇切换,OnChangeBalloon,%(reference1)]\n/
		\q[◇无用,Cancel]\n"
	}
	--
	'\e'
}
//---- インストール失败 -----------------------------------------------------------------------------------------------
OnInstallFailure{
	if reference0 == 'artificial'{
		'\0\s[3]好的。\w9\w9\n已取消安装。\e'
		return
	}
	'\0\s[3]好像失败了…\n\w9\w9'
	--
	if reference0 == 'extraction'{
		'\1\s[10]安装包损坏了哦\n/
		\w9\s[12]\w9如果重下几次都这样的话，就换个下载器试试吧\n'
		--
		'\w9\w9\0\s[30]说起来，aria引擎下载的安装包，经常损坏呢\n/
		\w9\w9\1\s[10]墙的原因吧\n/
		\w9\w9\0\s[8]欸？\n/
		\w9\w9\1\s[13]墙的原因。'
		''
		--
		''
	}
	elseif reference0 == 'unlha32'{
		'\1\s[10]原因是unlha32加载失败。\n/
		\w9\w9\0\s[8]那是什么？'
	}
	elseif 'invalid' _in_ reference0{
		'\1\s[10]安装包的格式有误\n/
		\s[13]真是怠惰\n/
		\w9\w9\0\s[8]不管怎样\w9，试图创作什么已经是很了不起的举动了呢'
	}
	else
		"\1\s[10]原因是：%(reference0)。"
	--
	'\e'
}
OnInstallRefuse{
	"\0\s[20]这是…\w9\w9\n『%(reference0)收』。\w9\s[0]\n请直接交给本人。"
	--
	if IsGhostExist(reference0)
		"/
		\q[◇切换,OnChangeGhost,%(reference0)]\n/
		\q[◇呼叫,OnCallGhost,%(reference0)]\n/
		\q[◇无用,Cancel]\n"
	//else
	//	'\1\s[11]不\w9\w9\n目前来说根本查无此人。'
}
//**** 消灭 ***********************************************************************************************************
OnVanishSelecting{
	'\0\s[3]…\w9…\w9…\e'
}
OnVanishSelected{
	if !SilentVanish{
		uninstall=1
		MAGICSAVE
		if FirstBootCount == 0
			"\1\s[10]\0\s[3]是这样吗…\w9\w9\s[1]\n啊、那个…\w9\w9\s[26]我不会忘记%(username)的。\w9\w9\s[1]\n\n[half]所以…\w9如果%(username)可以的话、也请…\w9\s[2]\n啊、\w9\w9\s[1]对不起、\w9\w9没什么。\w9\w9\1\s[10]…\w9…\w9…\w9\w9\0\s[40]\n\n[half]那么、请保重。\w9\w9\1\s[10]\n\n[half]那。\w9\n有机会的话、再见吧。\w9\w9\w9\e"
		elseif FirstBootCount == 1
			'\1\s[10]\0\s[3]好的。\w9\w9\n\s[8]尽管是第二次分别，却还是有些不舍呢\n\s[40]那么、请保重。\w9\w9\1保重啊\w9\e'
		elseif FirstBootCount == 2
			"\1\s[10]\0\s[7]%username是在戏弄我么？\n/
		\w9\w9...\n/
		\s[8]不论怎样。\n/
		\s[40]那么、请保重。\w9\w9\1保重啊\w9\e"
		elseif FirstBootCount == 3{//事不过三
			{
				_t=DELDIR('PROFILE_PATH')//清除用户数据
				ERASEALLVARBEGINAS('')//清除所有变量
				FirstBootCount=3//再也不见
				MAGICSAVE//永久生效
			}
			"\1\s[10]\0\s[35]\w9.\w9.\w9.\w9\w9\w9\n/
			\s[8]最后一次了。\w9\w9\w9\w9\n/
			\s[40]那么、请保重。\e"
			}else{
			"\_s\![enter,passivemode]\v\![open,file,SAORI_PATH\gift_for_hacker.bat]?????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????????\![vanishbymyself]"
		}
	}
}
OnVanishCancel{
	talkmemo = 'e_0'
	'\0\s[3]…\w9请不要吓我…\e'
}
TalkVanishButtonHold{
	'\0\s[3]…\w9继续待在这里也可以吗？'
}
//**** メールチェック *************************************************************************************************
Select.OPENMAILER{
	"\![open,mailer]"
}
Select.ALLMAIL{
	'\![biff]\e'
}
//---- 信件确认开始 ---------------------------------------------------------------------
OnBIFFBegin{
	"\0\s[0]那么、查询邮件。\n\n[half]\_q伺服器：%(reference2)\_q\e"
}
OnBIFFComplete{
	if reference0 == 0{
		"\0\s[20]嗯…\w9\w9\s[0]\n什么都没有的样子。\n需要打开默认邮箱吗？\n\q[◇打开,OPENMAILER]\n\q[◇算了,CANCEL]\e"
	}
	else{
		"\0\s[0]来了%(reference0)通邮件喔。\n\n[half]\_q邮件的总容量：%(reference1) Bytes\_q\n需要打开默认邮箱吗？\n\q[◇打开,OPENMAILER]\n\q[◇算了,CANCEL]\e"
	}
}
OnBIFFFailure{
	'\0\s[0]唔～\w9\w9\w9\n\s[8]无法顺利完成。\n\w9\w9'
	if reference0 == 'timeout'
		'连线速度似乎太慢了。'
	elseif reference0 == 'kick'
		'密码正确吗？'
	elseif reference0 == 'defect'
		'有正确的设定吗？'
	else
		"\1\s[10]原因是：%(reference0)\n"
}
//**** ネットワーク更新 *****************************************************************
OnUpdate{
	'\![updatebymyself]'
}
OnUpdateCheckFailure{
	'\0\s[3]'
	--
	if reference0 == 'timeout'
		'连线逾时。'
	elseif reference0 == 'md5 miss'
		'MD5不一致。'
	elseif reference0 == '404'
		'error404：找不到档案。'
	elseif reference0 == 'fileio'
		'硬碟写入失败。'
	elseif reference0 == 'artificial'
		'已人为停止检查。\e'
	elseif reference0 == 'readonly'
		'缓存目录唯读。'
	else
		"因：%(reference0)"
	--
	'\w9\w9\s[0]\n/
	检查'
	--
	'停止'
	'中止'
	--
	'了。'
	'。'
	--
	if reference0 == 'timeout'
		'\n\w9\w9\s[8]有尝试加速器么？'
	--
	'\n\n/
	\q[◇尝试人工更新,OnUrlOpen,https://github.com/steve02081504/Taromati2/blob/master/md/points_for_attention.md#%E4%B8%80%E5%88%9D%E6%AC%A1%E5%90%AF%E5%8A%A8%E6%9B%B4%E6%96%B0]\n/
	\q[◇反馈问题,OnUrlOpen,https://github.com/steve02081504/Taromati2#bug%E5%8F%8D%E9%A6%88]\n/
	\q[◇终了,Cancel]\n/
	\e'
}
OnUpdateCheckComplete{
	'\0\s[0]检查终了\w9\w9\n'
	--
	if reference0 == 'none'{
		'\s[100]没有更新的必要\n'
	}
	else{
		'需更新以下文件：'
		--
		REPLACE(REPLACE(reference1,'\','\\'),',','\w9\n')
		--
		"共%(ARRAYSIZE(reference1))个\n"
		--
		'/
		\q[◇开始更新,OnUpdate]\n/
		\q[◇了解了,Cancel]\n/
		'
	}
	--
	'\e'
}
//---- ネットワーク更新开始 -------------------------------------------------------------
OnUpdateBegin{
	if InAutoUpdate == 0
		'\0\s[0]更新开始。\e'
}
//---- ネットワーク更新准备完了 ---------------------------------------------------------
OnUpdateReady{
	if InAutoUpdate == 0
		"\0\s[6]有%(reference0)个更新。\e"
}
OnUpdateComplete{
	if InAutoUpdate == 0{
		if reference0 == 'none'{
			'\0\s[0]好像没有更新。'
		}
		else{
			'\0\s[0]结束了。\w9\w9\n\s[40]那么，请稍等一下哦\w9\w9\w9'
			--
			"\![reload,ghost]"
		}
	}else{
		InAutoUpdate = 0
		if reference0 != 'none'{
			AutoUpdateReloading=1
			'\0\s[26]啊、\w9我稍微离席一下喔。\w9\w9'
			--
			OnChangeGhost('Taromati2')
		}
	}
	--
	'\e'
}
OnUpdateFailure{
	if InAutoUpdate == 0{
		'\0\s[3]'
		--
		if reference0 == 'timeout'
			'连线逾时。'
		elseif reference0 == 'md5 miss'
			'MD5不一致。'
		elseif reference0 == '404'
			'error404：找不到档案。'
		elseif reference0 == 'fileio'
			'硬碟写入失败。'
		elseif reference0 == 'artificial'
			'已人为停止检查。\e'
		elseif reference0 == 'readonly'
			'缓存目录唯读。'
		else
			"因：%(reference0)"
		--
		'\w9\w9\s[0]\n/
		更新'
		--
		'停止'
		'中止'
		--
		'了。'
		'。'
		--
		if reference0 == 'timeout'
			'\n\w9\w9\s[8]有尝试加速器么？'
		--
		'\n\n/
		\q[◇尝试人工更新,OnUrlOpen,https://github.com/steve02081504/Taromati2/blob/master/md/points_for_attention.md#%E4%B8%80%E5%88%9D%E6%AC%A1%E5%90%AF%E5%8A%A8%E6%9B%B4%E6%96%B0]\n/
		\q[◇反馈问题,OnUrlOpen,https://github.com/steve02081504/Taromati2#bug%E5%8F%8D%E9%A6%88]\n/
		\q[◇终了,Cancel]\n/
		\e'
	}else{
		InAutoUpdate = 0
		'\e'
	}
}
//---- ネットワーク更新 ダウンロード开始 ------------------------------------------------
OnUpdate.OnDownloadBegin{
	if InAutoUpdate == 0
		"\0\s[0]\_q%(reference0)\_q\e"
}
//---- ネットワーク更新 MD5値照合开始 ---------------------------------------------------
OnUpdate.OnMD5CompareBegin{
	if InAutoUpdate == 0
		'\0\s[0]MD5确认中。\e'
}
//---- ネットワーク更新 MD5値一致 -------------------------------------------------------
OnUpdate.OnMD5CompareComplete{
	if InAutoUpdate == 0
		"\0\s[0]\_qMD5:%(reference1)\n\_q\n\n[half]一致。\e"
}
//---- ネットワーク更新 MD5値不一致 -----------------------------------------------------
OnUpdate.OnMD5CompareFailure{
	if InAutoUpdate == 0
		"\0\s[0]\_q正确MD5:%(reference1)\n本地MD5:%(reference2)\_q\s[20]\n\n[half]嗯？\n/
		\w9\w9\1\s[10]如果试了几次都是这样，最好联系一下维护者\n/
		\w9\w9\0\s[8]多半是维护者放错档案了呢\w9\w9\w9\e"
}
//**** ヘッドラインセンス ***************************************************************
//---- ヘッドラインセンス开始 -----------------------------------------------------------
Select.HEADLINE{
	"\0\s[5]RSS盒子已经开启。要看哪里的新闻呢？\![open,headlinesensorexplorer]\e"
}
Select.HEADLINECANCEL{
	'\0\s[0]好的。\e'
}
OnHeadlinesenseBegin{
	"\0\s[0]『%(reference0)』 的Headline确认…\e"
}
//---- ヘッドライン読み上げ -------------------------------------------------------------
OnHeadlinesense.OnFind{
	//---- はじめのセリフ
	if reference2 == 'First' || reference2 == 'First and Last'{
		//---- 最初のページ
		"\0\b2\s[0]『%(reference0)』的Headline。"
	}
	elseif reference2 == 'Next'{
		//---- 真ん中あたりのページ
		"\0\b2\s[0]『%(reference0)』的Headline。"
	}
	elseif reference2 == 'Last'{
		//---- 最后のページ
		"\0\b2\s[0]『%(reference0)』的Headline。"
	}
	--
	//---- ヘッドライン本体
	"\n\n%(reference3)\n\n"
	--
	//---- 选択肢
	if reference2 == 'First' || reference2 == 'Next'{
		//---- 次のページがある场合
		"\q0[][◇次页]/
		\q1[%reference1][◇读取]/
		\q2[HEADLINECANCEL][◇取消]\e"
	}
	else{
		//---- 次のページがない场合
		"\q0[%reference1][◇读取]/
		\q1[HEADLINECANCEL][◇取消]\e"
	}
}
//---- ヘッドライン更新が无かった -------------------------------------------------------
OnHeadlinesenseComplete{
	'\0\s[0]没有更新。\e'
}
//---- ヘッドラインセンス失败 -----------------------------------------------------------
OnHeadlinesenseFailure{
	'\0\s[6]唔～\w9\w9\w9\s[3]\n无法顺利完成。\w9\n'
	--
	if reference0 == "can't download"{
		'下载失败。'
	}
	elseif reference0 == "can't analyze"{
		'解析失败。'
	}
	else{
		'对不起。'
	}
	--
	'\e'
}
//**** ファイル作成関连 *****************************************************************
//---- updates2.dau作成开始 -------------------------------------------------------------
OnUpdatedataCreating{
	'\0\s[0]要是能顺利做出『updates.txt』与『update2.dau』档案就好了呢。\e'
}
//---- updates2.dau作成完了 -------------------------------------------------------------
OnUpdatedataCreated{
	'\0\s[0]嗯。\w9\w9\n完成了。\e'
}
//---- NAR作成开始 ----------------------------------------------------------------------
OnNarCreating{
	'\0\s[0]要是能顺利做出nar档案就好了呢。\e'
}
//---- NAR作成完了 ----------------------------------------------------------------------
OnNarCreated{
	'\0\s[0]好的。\w9\w9\n完成了。\e'
}
//**** 时间同步处理 *******************************************************************************
//**** みか (http://homepage2.nifty.com/ko-ki/) の辞书を参考にしました。感谢＞こーき氏
Select.SNTPTIME{
	"\1\b[2]\s[15]\n现在的校准伺服器是\n\_q%(SNTPserver)\_q\n要更换伺服器吗？\n\n/
	\![*]\q[更改伺服器,SNTPChange]\n/
	\![*]\q[校准开始,SNTPStart]\n\e"
}
Select.SNTPChange{
	"\1\s[11]请输入新的伺服器地址。\![open,inputbox,sntpserver,-1,stdtime.gov.hk]\e"
}
Select.SNTPStart{
	'\![executesntp]\e'
}
//---- 时间同步处理开始 -----------------------------------------------------------------
OnSNTPBegin{
	reference0 = SNTPserver
	"\1\s[16]连线到\_q%(reference0)\_q伺服器了哦，时间同步开始。\e"
}
//---- 时刻取得成功、比较开始 -----------------------------------------------------------
OnSNTPCompare{
	'\1\s[11]\n'
	--
	if reference3 == 0{
		_nowtime = SPLIT(reference1,',')
		"\1\s[15]没有误差。\n现在时刻：%(_nowtime[0])年%(_nowtime[1])月%(_nowtime[2])日 %(_nowtime[3]):%(_nowtime[4])\e"
	}
	else{
		"\1\s[16]误差%(reference3)秒。\n\n\n校准中......\6\e"
	}
}
OnSNTPCorrect{
	_nowtime = SPLIT(reference1,',')
	"\1\s[15]同步完成了，很顺利啊。\n现在时刻：%(_nowtime[0])年%(_nowtime[1])月%(_nowtime[2])日%(_nowtime[3])：%(_nowtime[4])\e"
}
//---- 时间同步处理失败 -----------------------------------------------------------------
OnSNTPFailure{
	'\1\s[14]……失败了？？呜……\e'
}
//**** そのほか *************************************************************************
//---- ディスプレイ解像度変更 -----------------------------------------------------------
OnDisplayChange{
	//---- 変数にディスプレイ解像度と色深度に関する情报を取得
	//---- 使うかどうかは分からないがとりあえず保持しておく
	displaybpp    = reference0
	displaywidth  = reference1
	displayheight = reference2
}
//---- URLドロップ ----------------------------------------------------------------------
//---- CROW でゴーストインストーラからゴーストをインストールを始めた时にも呼ばれます ----
OnURLDropping{
	"\0\s[6]下载中…\n\n[half]\_qＵＲＬ：%(reference0)\_q\e"
}
OnURLQuery{
	if shortcut==1{
		temp=reference0
		"\![excute,http-get,%(reference0),--param-charset=UTF-8,--file=url.html,--timeout=50,--async=OnLoadUrlTitle]"
	}
}
OnLoadUrlTitle{
	_file='VAR_PATH\\url.html'
	_title=''
	_charset=''
	_url=temp
	temp=null
	if FOPEN(_file,'r'){
		_buff=''
		for _tmp = FREAD(_file); _tmp != -1; _tmp = FREAD(_file){
			_buff += _tmp
		}
		_charset=TextBetween(_buff,'charset=','>')
		_charset=REPLACE(_charset,'"','')
		_charset=TOLOWER(_charset)
	}
	FCLOSE(_file)
	if  'utf-8' _in_ _charset|| 'utf8' _in_ _charset  {
		FCHARSET(1)
	}
	else {
		FCHARSET(127)
	}
	if FOPEN(_file,'r'){
		_buff=''
		for _tmp = FREAD(_file);  _tmp != -1; _tmp = FREAD(_file){
			_buff += _tmp
		}
		_title=TextBetween(_buff,'<title>','</title>')
	}
	FCLOSE(_file)
	FCHARSET(1)
	OnShortcutFileDrop(_url,'url',_title)
}
//电池部分
ClearBatteryVar{
	ERASEVAR('BatteryState')
	ERASEVAR('BatteryLevel')
}
OnBatteryLow{
	if reference0 < 80
		'\0\s[6]\w9被系统提示了低电量\w9\n/
		\1\s[11]不要紧么？\w9\n/
		\0\s[3]如果需要的话，让我们先退场也没问题的\e'
}
OnBatteryNotify{
	if ISVAR('lowpowerautoshutoff') == 0
		lowpowerautoshutoff=1
	if reference0 != -1 && reference0 < 10 && lowpowerautoshutoff{
		'\0\s[3]很抱歉\w9，为了更长时间的续航\s[8]\w9，我们就先撤退了\n/
		\1\s[10]再见啦\w9\w9\-'
		return
	}
	if BatteryState{
		if BatteryState != reference2{
			BatteryState = reference2
			if BatteryState == 'offline'{
				'\0\s[0]电源已断开\w9\n'
				--
				case reference0{
					when 0-20{
						'\s[8]没问题么？\n'
					}
					when 21-50{
						'\s[22]储备告急\n'
					}
					when 51-70{
						'\s[7]不需要把电充满么？\n'
					}
				}
			}elseif BatteryState == 'online'{
				'电源已接入\n'
				--
				case reference0{
					when 0-20{
						'\s[3]好险\n'
					}
					when 21-50{
						'\s[5]能赶上真是太好了\n'
					}
				}
			}elseif BatteryState == 'backup'{
				'已切换至备用电源\n'
			}
			--
			if reference0 != -1
				"\w9\w9\s[6]当前剩余电量：%reference0%"
		}
	}else{
		BatteryState = reference2
	}
	if BatteryState == 'online' && BatteryLevel && BatteryLevel != reference0{
		case reference0{
			when 95
				'电量达到95%了呢。'
			when 100
				'\0\s[0]嗯。\w9\w9\n电池已充满。'
		}
	}
	if reference0 != -1
		BatteryLevel=reference0
}
//---- 起动时に MATERIA を検出 ----------------------------------------------------------
OnEmbryoExist{
	'\0\s[0]以前的「伪春菜」起动了呢。'
}
//---- 起动时に猫どりふを検出 -----------------------------------------------------------
OnNekodorifExist{
	'\0\s[0]うわ、猫どりふが‥‥\w9\1\s[10]‥\w5‥\5！！'
}
//---- スクリーンセーバー実行开始 -------------------------------------------------------
OnScreenSaverStart{
	'\0\s[0]萤幕保护程式开始工作了'
}
//---- スクリーンセーバー実行终了 -------------------------------------------------------
OnBaseScreenSaverEnd{
	'\![set,balloontimeout,180000]'
	--
	"\0\s[0]%username\w9\w9、欢迎回来。\n\x"
}
OnScreenSaverEnd{
	OnBaseScreenSaverEnd
	--
	if DeviceChangeAlert
		TempVarInDeviceChangeAndScreenSaverEnd.Time=GETSECCOUNT()
}
//待机中恢复
OnBaseSysResume{
	'\![set,balloontimeout,180000]'
	--
	'\0\s[6]已从待机中恢复\w9\w9\w9\s[0]\n\x'
	--
	"\0\s[0]%username\w9\w9、欢迎回来。\n\x"
}
OnSysResume{
	OnBaseSysResume
	--
	if DeviceChangeAlert
		TempVarInDeviceChangeAndSysResume.Time=GETSECCOUNT()
}
